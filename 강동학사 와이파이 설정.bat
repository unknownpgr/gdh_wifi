:: 2018년 11월 7일
:: 강동학사에서 인터넷을 편리하게 사용하기 위해 제작함.
:: 수능 8일 남았지만 공부하기는 싫고 뭐라도 해야될 것 같아서, 기숙사 나가는 김에 선물삼아 만듬.
::

:: 프로그램을 실행하기 위해 관리자 권한을 얻는 로직. 작동하지 않을 수도 있음. 그럴 경우, 관리자 권한으로 시작하면 해결됨.
@echo off
echo 프로그램 시작 중...
>nul 2>&1 "%SYSTEMROOT%\system32\cacls.exe" "%SYSTEMROOT%\system32\config\system"
if '%errorlevel%' NEQ '0' (
    goto UACPrompt
) else ( goto gotAdmin )
:UACPrompt
    echo Set UAC = CreateObject^("Shell.Application"^) > "%temp%\getadmin.vbs"
    set params = %*:"=""
    echo UAC.ShellExecute "cmd.exe", "/c %~s0 %params%", "", "runas", 1 >> "%temp%\getadmin.vbs"
    "%temp%\getadmin.vbs"
    exit /B

:gotAdmin
pushd "%CD%"
    CD /D "%~dp0"

:: 사용자가 와이파이를 선택할 수 있는 분기문
:LOOP
cls
echo.
echo                     ========[ 강동학사 네트워크 설정 ] ========
echo.
echo.
echo 강동학사 와이파이에 연결 : A
echo 일반 연결로 복구 : B
echo 개발자 정보(오류 발생 시 확인할 것) : C
echo.
echo (대소문자 구별 없음)
echo.
echo  (A/B/C)?
setlocal
set /p str=입력 :
echo.
if /i "%str%" == "a" goto D
if /i "%str%" == "b" goto H
if /i "%str%" == "c" goto W
echo 잘못된 입력입니다.
timeout /t 2 /nobreak > nul
goto LOOP

:D

:: 책상 번호를 입력받아서, +90한 것을 IP 끝자리로 설정.
:: 예컨대 책상 번호가 46번이면, IP 끝자리는 146.
:: 쓸데없이 0~90번대 비워두는 건 혹시 몰라서. 나중에 교사용 컴퓨터나 다른 거 할당할 수도 있으니까.
:: 이렇게 하면 170번부터 250번까지 80개 비는데, 이건 스마트폰 연결용으로 비워 둠. 어플리케이션은 만들어 봤는데, 삼성폰에서 안 돌아가서 포기.
:: 이 경우에 스마트폰 연결은 자리 번호+170하면 됨.
:: 단 강동학사 정원이 80명이라는 것 가정 하에 작성함. 80명보다 늘어나면 답 없음.
:: 그리고 향후에 netsh 명령어 지원 안 되면 그땐 답 없음.

cls
echo.
echo ==============[ 강동학사 와이파이 설정 ]==============
echo.
echo   사용자의 강동학사 번호(책상번호)를 입력해주세요.
set /p num=입력 :
set /a num=%num%+90
echo.
echo 설정중...
netsh interface ip set address name="Wi-Fi" source=static addr=10.72.79.%num% mask=255.255.252.0 gateway=10.72.76.1 gwmetric=1 > nul
netsh -c int ip set dns name="Wi-Fi" source=static addr=188.219.1.1 register=PRIMARY > nul
netsh -c int ip add dns name="Wi-Fi" 168.126.63.1 > nul
echo.
echo  ::  와이파이를 강동학사에 맞추어 설정하였습니다.  ::
echo.
goto E

:H

:: 와이파이를 일반 설정으로 되돌림
netsh interface ip set address name="Wi-Fi" source=dhcp > nul
netsh -c int ip set dns name="Wi-Fi" source=dhcp > nul
reg delete HKLM\Software\Classes\cmdfile\shell /ve /f > nul
netsh interface ip set address name="이더넷" source=dhcp > nul
netsh -c int ip set dns name="이더넷" source=dhcp > nul

cls
echo.
echo ==============[ 일반 와이파이 모드 설정]==============
echo.
echo.
echo  :: 와이파이를 자동 설정(평상시)으로 설정했습니다. ::
echo.
goto E

:W
cls
echo.
echo =====================[ 개발자 정보 ]=====================
echo.
echo   2018년 현재는 재학중인데 내년엔 졸업생
echo.
echo  이메일 : Unknownpgr@gmail.com
echo.
echo  연결이 안 되거나 오류 발생 시, 위의 이메일로 보고해주시면 시간날 때 고칠 예정.
echo  오류가 났을 때, 인터넷 설정 정보 보기 버튼(마지막 화면에서 s)를 눌러 표시되는 정보를 첨부하면 도움이 됨.
echo  cmd 만질 줄 아는 사람이면 ipconfig /all한 것과 동일함.
echo  그리고 윈도우 버전도 알려주면 도움 됨. 내PC - 우클릭 - 속성 들어가면 제일 위에 대문짝만하게 있음.
echo.
echo.
echo  # 오류 발생 시, 메일 보내기 전에 아래를 확인할 것. #
echo.  
echo  1. 그냥 실행해서 안 되면, 우클릭해서 '관리자 권한으로 실행' 을 눌러본다.
echo. 2. 혹시 인터넷 자체에 연결이 안 된 건 아닌지 살펴본다. 본 프로그램은 강동학사 와이파이에 연결되어 있을 때 
echo.    아이피만 바꿔주는 프로그램이고, 와이파이(gangdong)에 연결하는 건 사감쌤한테서 비밀번호 받아서 확인해야 함.
echo  3. 설정 도중에 DNS 설정이 잘못되었다느니, 레지스트리 키가 없다느니 하면서 에러가 뜰 수도 있음.
echo     근데 연결하는 거랑은 관계가 없기 때문에, 웹브라우저에서 네이버 들어가서 접속되는지 확인해볼 것.
echo.
echo.
echo  # 혹시 프로그램에 이상한 게 들어있진 않은지 의심스러운 사람을 위해서 #
echo.
echo  - 본 프로그램은 bat파일로 작성되었기 때문에, 확장자를 bat에서 txt로 바꾸면 내용을 모두 수정 가능.
echo  - 관리자 권한은 netsh 명령어로 ip 및 dns를 바꾸기 위해 필요함.
echo.
pause
goto E

:E
timeout /t 2 /nobreak > nul
cls
echo.
echo 인터넷 설정 정보 보기 : s
echo 처음으로 : f
echo 그 외 아무 키나 누르면 알아서 꺼집니다.
set /p str=입력해주세요 :
if /i "%str%"=="s" goto info
if /i "%str%"=="f" goto LOOP
cls
goto EE

:info

cls
ipconfig /all
pause
goto E

:EE

endlocal
exit